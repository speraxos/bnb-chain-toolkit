plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'agency.erc8004'
version = '0.1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.web3j:core:4.12.3'
    implementation 'com.google.code.gson:gson:2.11.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'ERC-8004 Java SDK'
                description = 'Java SDK for ERC-8004 Trustless AI Agent Registries'
                url = 'https://github.com/nirholas/erc8004-agent-creator'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
            }
        }
    }
}
